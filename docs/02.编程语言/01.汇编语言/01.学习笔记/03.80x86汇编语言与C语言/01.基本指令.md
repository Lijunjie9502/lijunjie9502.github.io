---
title: 基本指令
date: 2021-04-12 06:49:07
permalink: /pages/135dcb/
categories: 
  - 汇编语言
tags: 
  - null
author: 
  name: 木子识时务
  link: https://github.com/sbwcwso
editLink: true
---

# 基本指令


```markmap
- [基本指令](#基本指令)
  - [汇编程序员眼中的处理器系统结构](#汇编程序员眼中的处理器系统结构)
  - [从 C 生成汇编代码](#从-c-生成汇编代码)
  - [汇编语言数据格式](#汇编语言数据格式)
  - [第一条汇编指令实例](#第一条汇编指令实例)
  - [数据传送指令](#数据传送指令)
    - [MOV](#mov)
      - [寻址模式](#寻址模式)
        - [简单的寻址模式](#简单的寻址模式)
        - [通用寻址形式](#通用寻址形式)
      - [符号扩展与零扩展](#符号扩展与零扩展)
    - [PUSH 与 POP](#push-与-pop)
  - [地址计算指令](#地址计算指令)
  - [整数计算指令](#整数计算指令)
  - [X86-32 与 X86-64 比较](#x86-32-与-x86-64-比较)
    - [X86-32 与 X86-64 的数据类型宽度](#x86-32-与-x86-64-的数据类型宽度)
    - [x86-64 的通用寄存器](#x86-64-的通用寄存器)
    - [x86-32 与 x86-64 汇编代码的不同之处](#x86-32-与-x86-64-汇编代码的不同之处)
    - [操作数为 64 位时的 x86-64 的汇编代码](#操作数为-64-位时的-x86-64-的汇编代码)
```

> [80386 参考手册](https://nju-projectn.github.io/i386-manual/toc.htm)

## 汇编程序员眼中的处理器系统结构

:::tip CPU 和内存空间结构示意图
![20210412071251-2021-04-12-07-12-51](https://cdn.jsdelivr.net/gh/Lijunjie9502/PicBed@master/20210412071251-2021-04-12-07-12-51.png)
:::

:::note 指令寄存器(PC Program Counter)
* 指向下一条指令的地址
* `EIP`(X86-32) 或 `RIP`(x86-64)
:::
:::note 寄存器堆(Registers)
:::
:::note 条件码(Condition Codes)
* 用于储存最近执行指令的结果状态信息
* 用于条件跳转指令的判断
:::
:::note 内存空间
* 以字节编码的连续存储空间
* 存放程序代码、数据、运行栈以及操作系统数据
:::

## 从 C 生成汇编代码

:::tip C 代码

```c
int sum(int x, int y)
{
  int t = x + y;
  return t;
}
```

:::

:::note 命令行编译
* `gcc -O2 -S sum.c -m32 -fno-asynchronous-unwind-tables`
  * `-O2` 是优化的级别
  * `-m32` 是选择 32 位的 CPU
  * `-S` 生成汇编代码
  * `-fno-asynchronous-unwind-tables` 禁止输出一些信息
    * [参考](https://cloud.tencent.com/developer/ask/102663/answer/182019)

:::warning 产生的汇编码与 gcc 的版本有很大的关系，具体以 ppt 中为准
:::

:::note 对应的 X86-32汇编(AT&T 汇编格式)

```nasm
sum:
  pushl  %ebp
  movl  %esp, %ebp
  movl  12(%ebp), %eax
  movl  8(%ebp), %edx
  addl  %edx, %eax
  leave
  ret
```

* `leave` 指令等价于

```nasm
movl %ebp,%esp
popl %ebp
```

:::



## 汇编语言数据格式

:::note 数据格式
* 在 X86-32 中，使用 “字(word)” 来表示 16 位整数类型，“双字”表示 32 位
* 汇编语言中没有数据类型，一般采用汇编指令后缀进行区分

:::tip X86-32中的各种数据格式

| C 声明            | Intel 数据类型 | 汇编指令后缀 | 大小(字节) |
|-----------------|:------------:|:--------:|:--------:|
| `char`          | 字节         | b      | 1      |
| `short`         | 字          | w      | 2      |
| `int`           | 双字         | l      | 4      |
| `long int`      | 双字         | l      | 4      |
| `long long int` | --         | --     | 4      |
| `char *`        | 双字         | l      | 4      |
| `float`         | 单精度        | s      | 4      |
| `double`        | 双精度        | l      | 8      |
| `long double`   | 扩展精度       | t      | 10/12  |

:::

## 第一条汇编指令实例

:::note C代码
* 两个整数(32位)相加
  
  ```c
  int x += y
  ```

:::

:::note 汇编代码
* 两个32位整数相加

  ```nasm
  addl  %edx, %eax
  ```

  * `l` 后缀表示是双字运算
  * 无符号/带符号整数加法运算的指令是一样的
  * 与 C 语言的操作数对应  
    `x: Register eax`  
    `y: Register edx`  
:::

:::note 机器码

```
01  d0
```

* 2 字节指令(二进制形式)
:::

## 数据传送指令

### MOV

:::tip 数据传递(AT&T 语法)
* `movl Source, Dest`
  * 将一个“双字”从 `Source` 移动到 `Dest`
  * 常见指令
    * `movb Source, Dest`
      * 将一个字节从 `Source` 移动到 `Dest`
    * `movw Source, Dest`
      * 将一个字从 `Source` 移动到 `Dest`
:::

:::note 允许的操作数类型
* 立即数 (`Imm`)：常整数
  * 如： `$0x400`, `$-533`
  * 可以被 `1` ， `2` 或 `4` 个字节表示
* 寄存器 (`Reg`)
  * `8` 个通用寄存器
* 存储器 (`Mem`)
  * `4` 个连续字节
  * 支持多种访存寻址模式
:::tip
针对的是 `movl` 指令
:::

:::details 8 个通用寄存器  
![20210412093155-2021-04-12-09-31-55](https://cdn.jsdelivr.net/gh/Lijunjie9502/PicBed@master/20210412093155-2021-04-12-09-31-55.png)
:::

:::note 数据传输指令支持的不同操作数类型组合
![20210412093411-2021-04-12-09-34-11](https://cdn.jsdelivr.net/gh/Lijunjie9502/PicBed@master/20210412093411-2021-04-12-09-34-11.png)  
:::tip
* **不能两个操作数都为内存地址！**
* 通用寄存器加括号是通用的寻址形式，参照 [通用寻址形式](#通用寻址形式)
  * 相当于 C 语言中的指针的间接访问操作符 `*`
:::

#### 寻址模式

##### 简单的寻址模式

:::note 间接寻址
* 寄存器 `R` 中指定内存地址， 则 `(R)` 相当于是 `Mem[Reg[R]]`
* 指令： `movl (%ecx), %eax`
:::tip
* 相当于 `%ecx` 中存放的是指针，设为 `p`
* `(%exc)` 相当于对指针进行解引用，即 `*p`
:::

:::note 基址+偏移量寻址
* 寄存器 `R` 中指定内存起始地址，常数 `D` 给出偏移量
  * `D(R)` 等价于 `Mem[Reg[R]+D]`
* 指令： `movl 8(%ebp), %edx`
:::

:::details 简单寻址模式实例
* C 代码
  
  ```c
  void swap(int *xp, int *yp)
  {
    int t0 = *xp;
    int t1 = *yp;
    *xp = t1;
    *yp = t0;
  }
  ```

* 汇编码

  ```nasm
  swap:
    ;Set up
    pushl   %ebp
    movl    %esp, %ebp
    pushl   %ebx

    ;Body
    movl    12(%ebp), %ecx      ; ecx = yp
    movl    8(%ebp),  %edx      ; edx = xp
    movl    (%ecx),   %eax      ; eax = (ecx) = *yp = t1
    movl    (%edx),   %ebx      ; ebx = (edx) = *xp = t0
    movl    %eax,     (%edx)    ; (edx) = *xp = eax = t1
    movl    %ebx,     (%ecx)    ; (exc) = *yp = ebx = t0

    ;Finish
    movl    -4(%ebp), %ebx
    movl    %ebp,     %esp
    popl    %ebp
    ret
  ```
  
  * 其中 Setup 与 Finish 与栈帧的操作相关，会在后续内容中介绍。Body 部分是函数的主体
  * 寄存器与 C 中变量的对应

    |Register|Variable|
    |:--|:--|
    | `%ecx`  | `yp`  |
    | `%edx`  | `xp`  |
    | `%eax`  | `t1`  |
    | `%ebx`  | `t0`  |

  * 初始时栈帧的情况  
    * ![20210823180255-2021-08-23-18-02-56](https://cdn.jsdelivr.net/gh/sbwcwso/PicBed@master/20210823180255-2021-08-23-18-02-56.png)
    * `%ebp` 指向的是栈帧的基址
      * 其往上可以访问父函数传递的参数
      * 往下可以访问函数内部的局部变量
    :::tip
    * 栈底在顶，栈顶在底
    * 栈顶的地址小，栈底的地址大
      * 入栈时，栈顶指针减小
      * 出栈时，栈顶指针增大
:::

##### 通用寻址形式

::: theorem 寻址形式
* `D(Rb, Ri, S)` ➡️ `Mem[Reg[Rb] + S * Reg[Ri] + D]`
  * `D`: 常量(地址偏移量)
  * `Rb`: 基址寄存器
    * 8 个通用寄存器之一
  * `Ri`: 索引寄存器
    * `%esp` 不能作为索引寄存器
    * 一般 `%ebp` 也不用作此用途
  * `S`: 比例因子
    * `1` ， `2` ， `4` ， or `8`
    * 缺省时为 `1`
:::


:::details 通用寻址模式实例
* 设寄存器 `%edx` 的值为 `0xf200`，寄存器 `%ecx` 的值为 `0x100`

| 地址表达式           | 地址计算               | 访存地址    |
|:---------------:|:------------------:|:-------:|
| 0x8             | 0x8                | 0x8     |
| 0x8(%edx)       | 0xf200 + 0x8       | 0xf208  |
| (%edx, %exc)    | 0xf200 + 0x100     | 0xf300  |
| (%edx, %ecx, 4) | 0xf200 + 4 * 0x100 | 0xf600  |
| 0x80(, %edx, 2) | 2 * 0xf200 + 0x80  | 0x1e480 |

:::tip
* 如果是 `$0x8`， 则表示立即数；而 `0x8` 表示的是内存地址
:::

#### 符号扩展与零扩展

::: tip 当 Source 占用字节小于 Dest 占用字节时，需要对 Source 中的数据进行扩展后，再传送到 Dest
:::

:::note 符号扩展 (MOVS S, D): 用符号位来进行扩展
* `movsbw Source, Dest`
  * 将做了符号扩展的字节传送到字
* `movsbl Source, Dest`
  * 将做了符号扩展的字节传送到双字
* `movswl Source, Dest`
  * 将做了符号扩展的字传送到双字
:::

:::note 零扩展 (MOVZ S, D): 用零来进行扩展
* `movzbw Source, Dest`
  * 将做了零扩展的字节传送到字
* `movzbl Source, Dest`
  * 将做了零扩展的字节传送到双字
* `movzwl Source, Dest`
  * 将做了零扩展的字传送到双字
:::

### PUSH 与 POP

:::tip %esp 指向当前运行栈的栈顶地址
:::

::: note push
* `pushl Source` 双字压栈
  * 实现过程
    1. `R[%esp]` $\leftarrow$ `R[%esp]-4`
    2. `M[R[%esp]]` $\leftarrow$ `Source`

:::details 图示
<pre class='user-defined'>
      D  O        BEFORE PUSH                  AFTER PUSH
      I  F     * 31          0 *           * 31          0 *
      R        |               |           |               |
      E  E     |-------+-------|           |-------+-------|
      C  X     |###############|           |###############|
      T  P     |-------+-------|           |-------+-------|
      I  A     |###############|           |###############|
      O  N     |-------+-------|&lt;--ESP     |-------+-------|
      N  S     |               |           |    OPERAND    |
         I     |-------+-------|           |-------+-------|&lt;--ESP
       | O     |               |           |               |
       | N     |-------+-------|           |-------+-------|
       |       |               |           |               |
       !       |-------+-------|           |-------+-------|
               |               |           |               |
               *               *           *               *
</pre>
:::

:::note pop
* `popl Dest` 双字出栈
  * 实现过程
    1. `Dest` $\leftarrow$ `M[R[%esp]]`
    2. `R[%esp]` $\leftarrow$ `R[%esp] + 4`
:::details 图示
<pre class='user-defined'>
       D  O         BEFORE POP                  AFTER POP
       I  F     * 31          0 *           * 31          0 *
       R        |               |           |               |
       E  E     |-------+-------|           |-------+-------|
       C  X     |###############|           |###############|
       T  P     |-------+-------|           |-------+-------|
       I  A     |###############|           |###############|
       O  N     |-------+-------|           |-------+-------|&lt;--ESP
       N  S     |    OPERAND    |           |               |
          I     |-------+-------|&lt;--ESP     |-------+-------|
        | O     |               |           |               |
        | N     |-------+-------|           |-------+-------|
        |       |               |           |               |
        !       |-------+-------|           |-------+-------|
                |               |           |               |
                *               *           *               *

</pre>
:::

## 地址计算指令

::: theorem 指令格式
* `leal Src, Dest`
  * `Src` 为地址计算表达式
    * 其形式与寻址计算相同，参照[通用寻址形式](#通用寻址形式)
  * 计算出来的地址赋给 `Dest`
  * `mov` 指令会访存`Src`，`leal` 不会
  * `lea` 即指 `Load Effective Address`
:::

:::details 地址计算指令使用示例
* 地址计算(无需访存)
  * 比如计算元素的地址， `p = &x[i];`
* 进行 `x + k * y, k=1,2,4, or 8` 这一类型的整数计算
  * 借助 `leal` 指令会比加减乘除指令快
  * [实例](#leal指令用于计算)
:::

## 整数计算指令

:::note 双操作数指令

| 指令格式              | 计算                |
|:-----------------|:-----------------|
| `addl   Src, Dest` | `Dest = Dest + Src` |
| `subl   Src, Dest` | `Dest = Dest - Src` |
| `imull  Src, Dest` | `Dest = Dest * Src` |
| `sall   Src, Dest` | `Dest = Dest << Src` <br>与 `shll` 等价|
| `sarl   Src, Dest` | `Dest = Dest >> Src`  <br>算术右移|
| `shrl   Src, Dest` | `Dest = Dest >> Src`  <br>逻辑右移|
| `xorl  Src, Dest` | `Dest = Dest ^ Src` |
| `andl  Src, Dest` | `Dest = Dest & Src` |
| `orl  Src, Dest` | `Dest = Dest | Src` |

:::

:::details SHL(Shift Logical Left) 和 SAL(Shift Arithmetic Left) 图示
<pre class='user-defined'>
                     OF   CF                OPERAND

       BEFORE SHL    X    X     10001000100010001000100010001111
       OR SAL

       AFTER SHL     1    1 &lt;-- 00010001000100010001000100011110 &lt;-- 0
       OR SAL BY 1

       AFTER SHL     X    0 &lt;-- 00100010001000100011110000000000 &lt;-- 0
       OR SAL BY 10
</pre>
:::

:::details SHR 图示
<pre class='user-defined'>
                                      OPERAND                  CF

       BEFORE SHR         10001000100010001000100010001111     X

       AFTER SHR    0----&gt;01000100010001000100010001000111----&gt;1
       BY 1

       AFTER SHR    0----&gt;00000000001000100010001000100010----&gt;O
       BY 10
</pre>
:::

:::details SAR 图示
<pre class='user-defined'>
                                  POSITIVE OPERAND             CF

       BEFORE SAR         01000100010001000100010001000111     X

       AFTER SAR    0----&gt;00100010001000100010001000100011----&gt;1
       BY 1

                                  NEGATIVE OPERAND             CF

       BEFORE SAR         11000100010001000100010001000111     X

       AFTER SAR    0----&gt;11100010001000100010001000100011----&gt;1
       BY 1
</pre>
:::

:::note 单操作数指令

| 指令格式              | 计算                |
|:-----------------|:-----------------:|
| `incl Dest` | `Dest = Dest + 1` |
| `decl Dest` | `Dest = Dest - 1` |
| `negl Dest` | `Dest = - Dest` |
| `notl Dest` | `Dest = ~ Dest` |

:::

<h5 id='leal指令用于计算'></h5>

:::details 实例1: 将 leal 指令用于计算

* C 代码

  ```c
  int arith(int x, int y, int z)
  {
    int t1 = x + y;
    int t2 = z + t1;
    int t3 = x  + 4;
    int t4 = y * 48;
    int t5 = t3 + t4;
    int rval = t2 * t5;
    return rval;
  }
  ```

* 汇编码

  ```nasm
  arith:
    ;Set up
    pushl %ebp
    movl  %esp, %ebp

    ;Body
    movl  8(%ebp), %eax         ; eax = x
    movl  12(%ebp), %edx        ; edx = y
    leal  (%edx,%eax), %ecx     ; ecx = edx + eax = x + y = t1
    leal  (%edx,%edx,2), %edx   ; edx = edx + edx * 2 = 3 * y
    sall  $4, %edx              ; edx = edx << 4 = edx * 16 = 48 * y = t4 
    addl  16(%ebp), %ecx        ; ecx = ecx + z = t1 + z = t2
    leal  4(%edx,%eax), %eax    ; eax = edx + eax + 4 = t4 + x + 4 = t4 + t3 = t5
    imull %edx, %eax            ; eax = eax * ecx = t5 * t2

    popl %ebp
    ret

  ```

  * 初始时栈帧的情况
    * ![20210823210029-2021-08-23-21-00-29](https://cdn.jsdelivr.net/gh/sbwcwso/PicBed@master/20210823210029-2021-08-23-21-00-29.png)

:::

:::details 实例2: 整数计算指令
* C 代码

  ```c
  int logical(int x, int y)
  {
    int t1 = x^y;
    int t2 = t1 >> 17;
    int mask = (1<<13) - 7;
    int rval = t2 & mask;
    return rval;
  }
  ```

* 汇编代码

  ```nasm
  logical:
    ;Set up
    pushl %ebp
    movl  %esp, %ebp

    ;Body
    movl  8(%ebp), %eax;    ; eax = x
    xorl  12(%ebp), %eax    ; eax = x ^ y = t1
    sarl  $17, %eax         ; eax = eax >> 17 = t1 >> 17 = t2 
    andl  $8185, %eax       ; eax = eax & 8185 = t2 & (1 << 13 - 7) = rval

    ;Finish
    movl %ebp, %esp
    popl %ebp
    ret
  ```

:::

## X86-32 与 X86-64 比较

### X86-32 与 X86-64 的数据类型宽度

::: details Size of C objects (in Bytes)
![20210412122258-2021-04-12-12-22-58](https://cdn.jsdelivr.net/gh/Lijunjie9502/PicBed@master/20210412122258-2021-04-12-12-22-58.png)
::: tip 主要是 <code>long int</code> 和 <code>long double</code> 以及指针类型的数据宽度不同
:::

### x86-64 的通用寄存器

:::note x86-64 与 x86-32 进行比较
* 其扩展了现有的寄存器，并增加了 8 个新的寄存器
  * x86-64 的指令属于变长指令
  * x86-32 的寄存器都包含在了 x86-64 部分寄存器的低 32 位中
* `%ebp/%rbp` 不再是专用寄存器

:::details x86-64的通用寄存器示意图
![20210412122440-2021-04-12-12-24-40](https://cdn.jsdelivr.net/gh/Lijunjie9502/PicBed@master/20210412122440-2021-04-12-12-24-40.png)
:::

### x86-32 与 x86-64 汇编代码的不同之处

:::details <code>swap</code> 的 C 代码

```c
void swap(int *xp, int *yp)
{
  int t0 = *xp;
  int t1 = *yp;
  *xp = t1;
  *yp = t0;
}
```

:::

:::details x86-32 下的 <code>swap</code> 的汇编码

```nasm
swap:
    ;Set up
    pushl   %ebp
    movl    %esp, %ebp
    pushl   %ebx

    ;Body
    movl    12(%ebp), %ecx      ; ecx = yp
    movl    8(%ebp),  %edx      ; edx = xp
    movl    (%ecx),   %eax      ; eax = (ecx) = *yp = t1
    movl    (%edx),   %ebx      ; ebx = (edx) = *xp = t0
    movl    %eax,     (%edx)    ; (edx) = *xp = eax = t1
    movl    %ebx,     (%ecx)    ; (exc) = *yp = ebx = t0

    ;Finish
    movl    -4(%ebp), %ebx
    movl    %ebp,     %esp
    popl    %ebp
    ret
```

:::

:::details x86-64下的 <code>swap</code> 的汇编代码

```nasm
swap:
  movl  (%rdi), %edx    ;edx = (%rdi) = *xp = t0
  movl  (%rsi), %eax    ;eax = (%rsi) = *yp = t1
  movl  %eax, (%rdi)    ;(%rdi) = *xp = %eax = t1
  movl  %edx, (%rsi)    ;(%rsi) = *yp = %edx = t0
  retq
```

* 第一个参数(`xp`) 由 `%rdi` 传递，第二个参数(`yp`)由 `%rsi` 传递
  * `xp`, `yp` 均为 64 位的指针
* 相当于是无栈操作

:::

:::note x86-64 与 x86-32 的不同点
  * x86-64 参数通过寄存器来传递
    * 当参数少于 7 个时，参数从左到右依次放入寄存器：`rdi, rsi, rdx, rcx, r8, r9`
    * 当参为 7 个以上时，前 6 个传递方式不变，但后面的依次从“右向左”放入栈中
  * 当参数少于 7 个时 x86-64 相当于是无栈操作

* 如果被操作的数据是 32 位
  * x86-64 仍可以继续使用寄存器 `%eax, %edx` 以及 `movl` 指令
:::

### 操作数为 64 位时的 x86-64 的汇编代码

:::details 操作数为 <code>long int</code> 的 <code>swap_1</code> 的 c 代码及其汇编码

```c
void swap(long int *xp, long int *yp)
{
  long int t0 = *xp;
  long int t1 = *yp;
  *xp = t1;
  *yp = t0;
}
```

```nasm
swap_1:
  movl  (%rdi), %rdx    ;rdx = (%rdi) = *xp = t0
  movl  (%rsi), %rax    ;rax = (%rsi) = *yp = t1
  movl  %rax, (%rdi)    ;(%rdi) = *xp = %rax = t1
  movl  %rdx, (%rsi)    ;(%rsi) = *yp = %rdx = t0
  retq
```

:::

:::note 被操作的数据是 64 位
* 所以使用 64 位的寄存器 `%rax`, `%rdx`
* 以及 `movq` 指令
  * `q` 表示 “4字”
:::
